# Maintainer: graysky <graysky@archlinux.us>
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

pkgbase=raspberrypi-bootloader
pkgname=('raspberrypi-bootloader-pikvm'
         'raspberrypi-bootloader-x-pikvm')
pkgver=20220419
pkgrel=11
_commit=9c649b15dc8ef92b2b10f851e412855563215788
arch=('any')
url="https://github.com/raspberrypi/firmware"
license=('custom')
options=(!strip)
source=("bootloader-$pkgver-${_commit:0:10}.tar.gz::https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz")
md5sums=('2a0da0a15ffce624ed5f7654cef5226f')

package_raspberrypi-bootloader-pikvm() {
  pkgdesc="Bootloader files for Raspberry Pi"
  replaces=('raspberrypi-firmware-bootloader')

  conflicts=(raspberrypi-bootloader)
  provides=("raspberrypi-bootloader=$pkgver")

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  rm "${pkgdir}"/boot/{start{_,4}x.elf,fixup{_,4}x.dat}
}

package_raspberrypi-bootloader-x-pikvm() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-bootloader')
  replaces=('raspberrypi-firmware-bootloader-x')

  conflicts=(raspberrypi-bootloader-x)
  provides=("raspberrypi-bootloader-x=$pkgver")

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start{_,4}x.elf,fixup{_,4}x.dat} "${pkgdir}"/boot
}
